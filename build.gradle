plugins {
    id "com.github.hierynomus.license" version "0.12.1"
}

apply plugin: 'java'
apply plugin: 'eclipse'

repositories {
    jcenter()
}

version = '0.0.1'
sourceCompatibility = 1.8

tasks.withType(JavaCompile) {
	options.compilerArgs << '-Xlint:all'
	options.encoding = 'UTF-8'
}

test {
	if(logger.infoEnabled) {
		testLogging.showStandardStreams = true
	}
	jvmArgs '-XX:+HeapDumpOnOutOfMemoryError', '-enableassertions'
}

dependencies {
}

license {
    header = file('gradle/license-header.txt')
    include "**/*.java"
    ext.year = 2016
    ext.name = 'Christoph Pirkl'
    ext.email = 'christoph at users.sourceforge.net'
}

eclipse {
	classpath {
		downloadSources = true
		containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
		def javaVersion = project.sourceCompatibility.toString()
		def container = "org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-$javaVersion"
		logger.info "Using container $container for $project"
		containers container
	}
	jdt.file {
		beforeMerged  { jdt ->
		  File defaultProperties = new File("${rootProject.projectDir}/gradle/defaultEclipseJdtPrefs.properties").absoluteFile
		  logger.info "Load defaults from $defaultProperties for $project"
		  jdt.load(defaultProperties)
		}
	}
}
